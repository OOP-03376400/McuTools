; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "MCUTools"
#define MyAppVersion "12/2015"
#define MyAppPublisher "webmaster442"
#define MyAppURL "http://www.example.com/"
#define MyAppExeName "MCUTools.Loader.exe"
#include <idp.iss>

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5A39446F-90E5-43BA-BED1-6A046405B09C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=c:\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
CreateUninstallRegKey=yes
UninstallDisplayIcon={uninstallexe}
InternalCompressLevel=max
MinVersion=0,6.0sp2
OutputDir=..\bin\setup
FlatComponentsList=False
PrivilegesRequired=lowest
LicenseFile=..\LICENSE

[UninstallDelete]
Type: filesandordirs; Name: "{app}\*"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\bin\Launcher\MCUTools.Loader.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\bin\Launcher\MCUTools.Loader.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "7z.dll"; Flags: dontcopy
Source: "7z.exe"; Flags: dontcopy
Source: "dotNetFx45_Full_setup.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall ignoreversion uninsremovereadonly
Source: "{tmp}\mcutools.7z"; Flags: deleteafterinstall external dontcopy; ExternalSize: 3661967
Source: "{tmp}\SOC.7z"; Flags: deleteafterinstall external dontcopy; ExternalSize: 21677560
Source: "{tmp}\arduino_1_5.7z"; Flags: deleteafterinstall external dontcopy; ExternalSize: 78046775
Source: "{tmp}\processing2.7z"; Flags: deleteafterinstall external dontcopy; ExternalSize: 74514021
Source: "{tmp}\LibreOfficePortable.7z"; Flags: deleteafterinstall external dontcopy; ExternalSize: 131804461

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\dotNetFx45_Full_setup.exe"; Parameters: "/passive"; Flags: waituntilterminated skipifdoesntexist shellexec hidewizard; Description: "Installing .net 4.5"; Check: IsDotNet4Detected
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Types]
Name: "Network"; Description: "Network run install";
Name: "Local"; Description: "Local Install / File Share Install";
Name: "Full"; Description: "Full Install";
Name: "Custom"; Description: "Custom Install"; Flags: iscustom

[Components]
Name: "Loader"; Description: "Launcher App"; Types: Network Local Full Custom; Flags: fixed
Name: "Core"; Description: "MCU Tools"; ExtraDiskSpaceRequired: 67084768; Types: Full Local
Name: "SOC"; Description: "SOC Development Tools";  ExtraDiskSpaceRequired: 81281230; Types: Full Local
Name: "Apps"; Description: "Applications"; Types: Full
Name: "Apps\Arduino15"; Description: "Arduino IDE 1.5"; ExtraDiskSpaceRequired: 634576236; Types: Full
Name: "Apps\LibreOffice"; Description: "Libre Office"; ExtraDiskSpaceRequired: 389685812; Types: Full
Name: "Apps\Processing"; Description: "Processing 2"; ExtraDiskSpaceRequired: 215432573; Types: Full

[Code]
//-----------------------------------------------------------------------------
const
  Dload_MCU = 'https://googledrive.com/host/0BzRxNHSnXbB5QjJZRG1iQWwyR00/install/mcutools.7z';
  Inst_MCU = '{tmp}\mcutools.7z';
  Dload_SOC = 'https://googledrive.com/host/0BzRxNHSnXbB5QjJZRG1iQWwyR00/install/SOC.7z';
  Inst_SOC = '{tmp}\SOC.7z';
  Dload_Soft_Arduino = 'https://googledrive.com/host/0BzRxNHSnXbB5QjJZRG1iQWwyR00/install/SoftWare/arduino_1_5.7z';
  Inst_Soft_Arduino = '{tmp}\arduino_1_5.7z';
  Dload_Soft_Processing = 'https://googledrive.com/host/0BzRxNHSnXbB5QjJZRG1iQWwyR00/install/SoftWare/processing2.7z';
  Inst_Soft_Processing = '{tmp}\processing2.7z';
  Dload_Soft_LibreOffice = 'https://googledrive.com/host/0BzRxNHSnXbB5QjJZRG1iQWwyR00/install/SoftWare/LibreOfficePortable.7z';
  Inst_Soft_LibreOffice = '{tmp}\LibreOfficePortable.7z';
//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, release, serviceCount: cardinal;
    check45, success: boolean;
begin
    // .NET 4.5 installs as update to .NET 4.0 Full
    if version = 'v4.5' then begin
        version := 'v4\Full';
        check45 := true;
    end else
        check45 := false;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0/4.5 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 uses additional value Release
    if check45 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= 378389);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;

function IsDotNet4Detected(): boolean;
begin
  result := not IsDotNetDetected('v4.5', 0);
end;

//-----------------------------------------------------------------------------
function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstallString: String;
begin
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;

//-----------------------------------------------------------------------------
function UnInstallOldVersion(): Integer;
var
  sUnInstallString: String;
  iResultCode: Integer;
begin
// Return Values:
// 1 - uninstall string is empty
// 2 - error executing the UnInstallString
// 3 - successfully executed the UnInstallString

  // default return value
  Result := 0;

  // get the uninstall string of the old app
  sUnInstallString := GetUninstallString();
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString, '/SILENT /NORESTART /SUPPRESSMSGBOXES','', SW_HIDE, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else
      Result := 2;
  end else
    Result := 1;
end;

procedure InitializeWizard;
begin
  idpDownloadAfter(wpReady);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
Result := True;
  if CurPageID = wpSelectTasks then begin
    if IsComponentSelected('Core') then idpAddFile(Dload_MCU, ExpandConstant(Inst_MCU));
    if IsComponentSelected('SOC') then idpAddFile(Dload_SOC, ExpandConstant(Inst_SOC));
    if IsComponentSelected('Apps\Arduino15') then idpAddFile(Dload_Soft_Arduino,  ExpandConstant(Inst_Soft_Arduino));
    if IsComponentSelected('Apps\Processing') then idpAddFile(Dload_Soft_Processing, ExpandConstant(Inst_Soft_Processing));
    if IsComponentSelected('Apps\LibreOffice') then idpAddFile(Dload_Soft_LibreOffice,  ExpandConstant(Inst_Soft_LibreOffice));
  end;
end;


procedure Extract(file: String; Caption: TNewStaticText);
var
  ErrorCode: Integer;
begin
  if FileExists(expandconstant(file)) then begin
      Caption.Caption := 'Extracting: '+file;
      ShellExec('', ExpandConstant('{tmp}\7z.exe'), 'x -o'+ExpandConstant('{app} ')+ExpandConstant(file), '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);
  end;
end;

procedure ExtractTo(file: String; targetdir: string; Caption: TNewStaticText);
var
  ErrorCode: Integer;
begin
  if FileExists(expandconstant(file)) then begin
      Caption.Caption := 'Extracting: '+file;
      ShellExec('', ExpandConstant('{tmp}\7z.exe'), 'x -o'+ExpandConstant(targetdir+' ')+ExpandConstant(file), '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);
  end;
end;  

procedure CurStepChanged(CurStep: TSetupStep);
var
  Page: TWizardPage;
  ProgressBar: TNewProgressBar;
  StaticText: TNewStaticText;
begin
  if (CurStep=ssInstall) then
  begin
    if (GetUninstallString() <> '') then UnInstallOldVersion();
        Page := PageFromID(wpInstalling);
        ProgressBar := TNewProgressBar.Create(Page);
        ProgressBar.Top := 100;
        ProgressBar.Width := Page.SurfaceWidth;
        ProgressBar.Parent := Page.Surface;
        ProgressBar.Style := npbstMarquee;
      
        StaticText := TNewStaticText.Create(Page);
        StaticText.Top := 80;
        StaticText.Caption := '';
        StaticText.AutoSize := True;
        StaticText.Parent := Page.Surface;
  
        ExtractTemporaryFile('7z.exe');
        ExtractTemporaryFile('7z.dll');

        Extract(Inst_MCU, StaticText);
        Extract(Inst_SOC, StaticText);
        ExtractTo(Inst_Soft_Arduino, '{app}\software', StaticText);
        ExtractTo(Inst_Soft_Processing, '{app}\software', StaticText);
        ExtractTo(Inst_Soft_LibreOffice, '{app}\software', StaticText);

        ProgressBar.Visible := false;
        StaticText.Visible := false;
  end;
end;
